package com.jbrod.servidorprincipal.analizadores;
import java_cup.runtime.*;

parser code{:
    
    public String resultado = "";

    // Conectar el parser al escaner
    public Parser(Lexer lex){
        super(lex);
    }

    public void syntax_error(Symbol cur_token){
        System.out.println("Simbolo con error:" + symbl_name_from_id(cur_token.sym));
        System.out.println("Linea " + cur_token.left);
        System.out.println("Columna " + cur_token.right);
        if (expected_token_ids().isEmpty()) {
            System.out.println("Ya no se esperaba ningun simbolo");
            //reporteErrores.agregarError(symbl_name_from_id(cur_token.sym),cur_token.left,cur_token.right, "Sintactico", "Ya no se esperaba ningun simbolo." );
        }else{
            //reporteErrores.agregarError(symbl_name_from_id(cur_token.sym),cur_token.left,cur_token.right, "Sintactico", "Se esperaba otro simbolo." );
        }
    }

    /*public void unrecovered_syntax_error(Symbol cur_token) {
        System.out.println("Error irrecuperable sobrecargado");
    }*/

   private String secureString(Object o){
        if( o != null){
            return o.toString();
        }else{
            return "";
        }
   }

:}

/* - - - - - - - - - - - - - - - - - - - - - - - Terminales - - - - - - - - - - - - - - - - - - - - - - - */
/* Numeros, Identificadores, Strings */
terminal NUMERO, IDENTIFICADOR, STRING, ENCABEZADO;

/* Operadores */
terminal MENQUE, MAYQUE, EXCLAM, DOSPUN, LLAVOP, LLAVCL, COROPN, CORCLS, COMMA, QSTMRK, COMILL, ORSYMB;

/* Encabezados de solicitudes */
terminal REALIZAR_SOL, FIN_SOL_REAL;

/* Usuarios */
terminal USUARIO_NUEV, DATOS_USUARI, USUARIO, PASSWORD, NOMBRE, INSTITUCION;

/* Modificar usuarios */
terminal MODI_USUARIO, USUA_ANTIGUO, NUEVO_PASSWO, ELIMINAR_USU;

/* Login usuario */
terminal LOGIN_USUARI;

/* Trivias */
terminal NUEVA_TRIVIA, PARAM_TRIVIA, ID_TRIVIA, TIEMPO_PREGU, TEMA;

/* Modificar trivias */
terminal CAMPO_TEXTO, AREA_TEXTO, CHECKBOX, RADIO, FICHERO, COMBO;

terminal ELIMINAR_TRI, MODIF_TRIVIA, AGREGAR_COMP, ELIMINAR_COM, MODIFICAR_CM, PARAMETROS_C, ID, TRIVIA, RESPUESTA, CLASE, TEXTO_VISIBL, OPCIONES, INDICE, FILAS, COLUMNAS;

/* - - - - - - - - - - - - - - - - - - - - - - No terminales - - - - - - - - - - - - - - - - - - - - - - */
non terminal inicio; 

/* Solicitudes */
non terminal solicitud, solicitudes; 

non terminal solicitud_simple, usuario_nuevo, modificar_usuario, eliminar_usuario, login_usuario;
non terminal nueva_trivia, eliminar_trivia, modificar_parametros_trivia, agregar_componente_trivia, eliminar_componente_trivia, modificar_componente_trivia; 

/* Auxiliares */
non terminal id_st;
non terminal opciones; 

/* Componentes */
non terminal componente_trivia ,campo_texto, area_texto, checkbox, radio, fichero, combo; 

/* - - - - - - - - - - - - - - - - - - - - - - - Producciones - - - - - - - - - - - - - - - - - - - - - - - */
start with inicio; 
inicio ::= solicitud_simple;  
inicio ::= solicitudes;

solicitud_simple ::= usuario_nuevo | modificar_usuario | eliminar_usuario | login_usuario;
solicitud_simple ::= nueva_trivia | eliminar_trivia | modificar_parametros_trivia | agregar_componente_trivia | eliminar_componente_trivia | modificar_componente_trivia;
solicitudes ::= solicitud_simple solicitud | /* nada */;

id_st ::= IDENTIFICADOR:id {: RESULT = id; :} 
        | STRING:st {: RESULT = st; :};

/* Usuarios */
usuario_nuevo ::= MENQUE EXCLAM REALIZAR_SOL DOSPUN COMILL USUARIO_NUEV COMILL MAYQUE LLAVOP COMILL DATOS_USUARI COMILL DOSPUN COROPN LLAVOP
            COMILL USUARIO COMILL DOSPUN COMILL IDENTIFICADOR:id COMILL COMMA
            COMILL PASSWORD COMILL DOSPUN COMILL id_st:ps COMILL COMMA
            COMILL NOMBRE COMILL DOSPUN COMILL id_st:nm COMILL COMMA
            COMILL INSTITUCION COMILL DOSPUN COMILL id_st:in COMILL 
            LLAVCL CORCLS LLAVCL MENQUE FIN_SOL_REAL EXCLAM MAYQUE{: 
                resultado += "Usuario nuevo: " +  id.toString() + " " + ps.toString() + " " + nm.toString() + " " + in.toString () + "\n";
            :};

modificar_usuario ::= MENQUE EXCLAM REALIZAR_SOL DOSPUN COMILL MODI_USUARIO COMILL MAYQUE
                LLAVOP COMILL DATOS_USUARI COMILL DOSPUN COROPN LLAVOP
                COMILL USUA_ANTIGUO COMILL DOSPUN COMILL IDENTIFICADOR:id COMILL COMMA 
                COMILL USUARIO_NUEV COMILL DOSPUN COMILL IDENTIFICADOR:nid COMILL COMMA
                COMILL NUEVO_PASSWO COMILL DOSPUN COMILL id_st:ps COMILL 
                LLAVCL CORCLS LLAVCL MENQUE FIN_SOL_REAL EXCLAM MAYQUE{: 
                resultado += "Modificar usuario: " + id.toString() + " " + nid.toString() + " " + ps.toString() + "\n";
            :};

eliminar_usuario ::= MENQUE EXCLAM REALIZAR_SOL DOSPUN COMILL ELIMINAR_USU COMILL MAYQUE
                LLAVOP COMILL DATOS_USUARI COMILL DOSPUN COROPN LLAVOP
                COMILL USUARIO COMILL DOSPUN COMILL IDENTIFICADOR:id COMILL
                LLAVCL CORCLS LLAVCL MENQUE FIN_SOL_REAL EXCLAM MAYQUE{: 
                resultado += "Eliminar usuario: " + id.toString() + " \n";
            :};

login_usuario ::= MENQUE EXCLAM REALIZAR_SOL DOSPUN COMILL LOGIN_USUARI COMILL MAYQUE
                LLAVOP COMILL DATOS_USUARI COMILL DOSPUN COROPN LLAVOP
                COMILL USUARIO COMILL DOSPUN COMILL IDENTIFICADOR:id COMILL COMMA
                COMILL PASSWORD COMILL DOSPUN COMILL id_st:ps COMILL
                LLAVCL CORCLS LLAVCL MENQUE FIN_SOL_REAL EXCLAM MAYQUE{: 
                resultado += "Login: " + id.toString() + " " + ps.toString() + " \n";
            :};


/* Trivias */
nueva_trivia ::= MENQUE EXCLAM REALIZAR_SOL DOSPUN COMILL NUEVA_TRIVIA COMILL MAYQUE
                LLAVOP COMILL PARAM_TRIVIA COMILL DOSPUN COROPN LLAVOP 
                COMILL ID_TRIVIA COMILL DOSPUN COMILL IDENTIFICADOR:id COMILL COMMA
                COMILL TIEMPO_PREGU COMILL DOSPUN STRING:tp COMMA
                COMILL NOMBRE COMILL DOSPUN COMILL id_st:nm COMILL COMMA
                COMILL TEMA COMILL DOSPUN COMILL id_st:tm COMILL
                LLAVCL CORCLS LLAVCL MENQUE FIN_SOL_REAL EXCLAM MAYQUE{: 
                resultado += "Nueva trivia: " + id.toString() + " " + tp.toString()+ " " + nm.toString() + " " + tm.toString() + " \n";
            :};

eliminar_trivia ::= MENQUE EXCLAM REALIZAR_SOL DOSPUN COMILL ELIMINAR_TRI COMILL MAYQUE
                LLAVOP COMILL PARAM_TRIVIA COMILL DOSPUN COROPN LLAVOP 
                COMILL ID_TRIVIA COMILL DOSPUN COMILL IDENTIFICADOR:id COMILL 
                LLAVCL CORCLS LLAVCL MENQUE FIN_SOL_REAL EXCLAM MAYQUE{: 
                resultado += "Eliminar trivia: " + id.toString() + " \n";
            :};

modificar_parametros_trivia ::= MENQUE EXCLAM REALIZAR_SOL DOSPUN COMILL MODIF_TRIVIA COMILL MAYQUE
                LLAVOP COMILL PARAM_TRIVIA COMILL DOSPUN COROPN LLAVOP 
                COMILL ID_TRIVIA COMILL DOSPUN COMILL id_st:id COMILL COMMA
                COMILL TIEMPO_PREGU COMILL DOSPUN STRING:tp COMMA
                COMILL NOMBRE COMILL DOSPUN COMILL id_st:nm COMILL COMMA
                COMILL TEMA COMILL DOSPUN COMILL id_st:tm COMILL 
                LLAVCL CORCLS LLAVCL MENQUE FIN_SOL_REAL EXCLAM MAYQUE{: 
                resultado += "Modificar trivia: " + id.toString() + " " + tp.toString()+ " " + nm.toString() + " " + tm.toString() + " \n";
            :};

eliminar_componente_trivia ::= MENQUE EXCLAM REALIZAR_SOL DOSPUN COMILL ELIMINAR_COM COMILL MAYQUE
                LLAVOP COMILL PARAMETROS_C COMILL DOSPUN COROPN LLAVOP 
                COMILL ID COMILL DOSPUN COMILL IDENTIFICADOR:cm COMILL COMMA
                COMILL TRIVIA COMILL DOSPUN COMILL IDENTIFICADOR:tr COMILL
                LLAVCL CORCLS LLAVCL MENQUE FIN_SOL_REAL EXCLAM MAYQUE{: 
                resultado += "Eliminar componente: " + cm.toString() + " " + tr.toString() + " \n";
            :};



/* Hacer que los componentes devuelvan el componente creado y utilizarlo en base a lo que se necesite */
agregar_componente_trivia ::=  MENQUE EXCLAM REALIZAR_SOL DOSPUN COMILL AGREGAR_COMP COMILL MAYQUE
                LLAVOP COMILL PARAMETROS_C COMILL DOSPUN COROPN LLAVOP
                componente_trivia:c
                LLAVCL CORCLS LLAVCL MENQUE FIN_SOL_REAL EXCLAM MAYQUE{: 
                resultado += "Agregar componente: " + c.toString() + " \n";
            :};

modificar_componente_trivia ::= MENQUE EXCLAM REALIZAR_SOL DOSPUN COMILL MODIFICAR_CM COMILL MAYQUE
                LLAVOP COMILL PARAMETROS_C COMILL DOSPUN COROPN LLAVOP
                componente_trivia:c
                LLAVCL CORCLS LLAVCL MENQUE FIN_SOL_REAL EXCLAM MAYQUE{: 
                resultado += "Agregar componente: " + c.toString() + " \n";
            :};


    opciones ::= id_st:op {: RESULT = op.toString(); :}; 
    opciones ::= id_st:op ORSYMB opciones:opcs {: RESULT = op.toString() + "|" + opcs.toString(); :}; 

    componente_trivia ::= campo_texto:r {:RESULT = r;:}| area_texto:r {:RESULT = r;:} | checkbox:r {:RESULT = r;:} | radio:r {:RESULT = r;:} | fichero:r {:RESULT = r;:} | combo:r {:RESULT = r;:};

    campo_texto ::= COMILL ID           COMILL DOSPUN COMILL IDENTIFICADOR:id COMILL COMMA
                    COMILL TRIVIA       COMILL DOSPUN COMILL IDENTIFICADOR:tv COMILL COMMA
                    COMILL CLASE        COMILL DOSPUN COMILL CAMPO_TEXTO      COMILL COMMA
                    COMILL INDICE       COMILL DOSPUN        id_st:in                COMMA 
                    COMILL TEXTO_VISIBL COMILL DOSPUN COMILL id_st:tx         COMILL COMMA
                    COMILL RESPUESTA    COMILL DOSPUN COMILL id_st:rs         COMILL 
                    {: RESULT =  "Campo de texto: " + secureString(id) + " " + secureString(tv)+ " " + secureString(in)+ " " + secureString(tx)+ " " + secureString(rs) + "\n"; :};

    area_texto ::=  COMILL ID           COMILL DOSPUN COMILL IDENTIFICADOR:id COMILL COMMA
                    COMILL TRIVIA       COMILL DOSPUN COMILL IDENTIFICADOR:tv COMILL COMMA
                    COMILL CLASE        COMILL DOSPUN COMILL AREA_TEXTO       COMILL COMMA
                    COMILL INDICE       COMILL DOSPUN        id_st:in                COMMA 
                    COMILL TEXTO_VISIBL COMILL DOSPUN COMILL id_st:tx         COMILL COMMA
                    COMILL RESPUESTA    COMILL DOSPUN COMILL id_st:rs         COMILL COMMA
                    COMILL FILAS        COMILL DOSPUN        id_st:fl                COMMA
                    COMILL COLUMNAS     COMILL DOSPUN        id_st:cl          
                    {: RESULT =  "Area de texto: " + secureString(id) + " " + secureString(tv)+ " " + secureString(in)+ " " + secureString(tx)+ " " + secureString(rs) +  " " + secureString(fl)+ " " + secureString(cl) + "\n"; :};

    checkbox ::=    COMILL ID           COMILL DOSPUN COMILL IDENTIFICADOR:id COMILL COMMA
                    COMILL TRIVIA       COMILL DOSPUN COMILL IDENTIFICADOR:tv COMILL COMMA
                    COMILL CLASE        COMILL DOSPUN COMILL CHECKBOX         COMILL COMMA
                    COMILL INDICE       COMILL DOSPUN        id_st:in                COMMA 
                    COMILL TEXTO_VISIBL COMILL DOSPUN COMILL id_st:tx         COMILL COMMA
                    COMILL RESPUESTA    COMILL DOSPUN COMILL id_st:rs         COMILL COMMA
                    COMILL OPCIONES     COMILL DOSPUN COMILL opciones:op      COMILL 
                    {: RESULT =  "Checkbox: " + secureString(id) + " " + secureString(tv)+ " " + secureString(in)+ " " + secureString(tx)+ " " + secureString(rs) +  " " + " OPCIONES: " + secureString(op) + "\n"; :};

    radio    ::=    COMILL ID           COMILL DOSPUN COMILL IDENTIFICADOR:id COMILL COMMA
                    COMILL TRIVIA       COMILL DOSPUN COMILL IDENTIFICADOR:tv COMILL COMMA
                    COMILL CLASE        COMILL DOSPUN COMILL RADIO            COMILL COMMA
                    COMILL INDICE       COMILL DOSPUN        id_st:in                COMMA 
                    COMILL TEXTO_VISIBL COMILL DOSPUN COMILL id_st:tx         COMILL COMMA
                    COMILL RESPUESTA    COMILL DOSPUN COMILL id_st:rs         COMILL COMMA
                    COMILL OPCIONES     COMILL DOSPUN COMILL opciones:op      COMILL 
                    {: RESULT =  "Radio: " + secureString(id) + " " + secureString(tv)+ " " + secureString(in)+ " " + secureString(tx)+ " " + secureString(rs) +  " " + " OPCIONES: " + secureString(op) + "\n"; :};

    fichero ::=     COMILL ID           COMILL DOSPUN COMILL IDENTIFICADOR:id COMILL COMMA
                    COMILL TRIVIA       COMILL DOSPUN COMILL IDENTIFICADOR:tv COMILL COMMA
                    COMILL CLASE        COMILL DOSPUN COMILL FICHERO          COMILL COMMA
                    COMILL INDICE       COMILL DOSPUN        id_st:in                COMMA 
                    COMILL TEXTO_VISIBL COMILL DOSPUN COMILL id_st:tx         COMILL 
                    {: RESULT =  "Fichero: " + secureString(id) + " " + secureString(tv)+ " " + secureString(in)+ " " + secureString(tx) + "\n"; :};
    
    combo ::=       COMILL ID           COMILL DOSPUN COMILL IDENTIFICADOR:id COMILL COMMA
                    COMILL TRIVIA       COMILL DOSPUN COMILL IDENTIFICADOR:tv COMILL COMMA
                    COMILL CLASE        COMILL DOSPUN COMILL COMBO            COMILL COMMA
                    COMILL INDICE       COMILL DOSPUN        id_st:in                COMMA 
                    COMILL TEXTO_VISIBL COMILL DOSPUN COMILL id_st:tx         COMILL COMMA
                    COMILL RESPUESTA    COMILL DOSPUN COMILL id_st:rs         COMILL COMMA
                    COMILL OPCIONES     COMILL DOSPUN COMILL opciones:op      COMILL 
                    {: RESULT =  "Combo: " + secureString(id) + " " + secureString(tv)+ " " + secureString(in)+ " " + secureString(tx)+ " " + secureString(rs) +  " " + " OPCIONES: " + secureString(op) + "\n"; :};

